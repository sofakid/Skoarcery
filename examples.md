Skoar Examples
==============

# MIDI example - [listen on soundcloud](https://soundcloud.com/lucas-cornelisse/windwaker-sv1)

    <? Zelda Wind Waker Theme - inspired by piano arrangement by Shinobu Amayake ?>

    @midi => @type
    -1 => @transpose

    9/8 120 => ).
    mp

    | <_a,_c#,d> )). }. | d )). }. | <_a,_c#,d> )). }. | _a )). }. |

    | _a)   d] f#)   e] d)     c#] | d)  _b] _g)  c#] _a] d] _b]       | c#)   _a] _g)   d] _b)    c#] | d)   e]] ]] f#] d] e] _a)   ] |
    | _a)  d]] e]] f#)  e] d)  c#] | d)  _b] _g)  c#] _a] d] _b]       | c#)  d] e)  f#]] ]] g] e] c#] | d)   oo/ e]] d).      _a)   ] |
    | _a)  d]] e]] f#)  e] d)  c#] | e]] d].  _b] _g)  c#] _a] d] _b]  | c#)   _a] _g)   d] _b)    c#] | ]] d]] e]] ]] f#] d] e] _a) ] |
    | _a)  d]] e]] f#)  e] d)  c#] | d)  _b] _g)  c#] _a] d] _b]       | c#)  d] e)  f#]] ]] g] e] c#] | d)   oo/ e]] d).       a)   ] |

    |: g] f#] e] f#)  d] c#] d] e] | a] d] ] g] d] ] f#)    ] | g] a] b] b] g] e] a)  a]   | g] f#] d] e] f#] e]] f#]] e)  ] |
    |  d] c#] d] e)   e] a] g] c#] | g] d] ] f#)  ] e] c#] e] | d] b] d] g] a] b] f# d] g] | e).    ] f#] e] a)           ] :|

    | <_a,_c#,d> )). }. | d )). }. | <_a,_c#,d> )). }. | <_a,_f#> )). _a ) ] |

This was played over MIDI to my Korg SV1.

# Drumming example - [listen on soundcloud](https://soundcloud.com/lucas-cornelisse/beets)

    <? 4/4 time, 70 beats per min. No voice label on this line, all voices will read it ?>

    4/4 70 => )


    <? each line begins with a voice label, we are using three voices ?>
    .h  @hats  => @instrument
    .s  @snare => @instrument forte
    .k  @kick  => @instrument

    .h  | }}}     |
    .s  | } ) } ) |
    .k  | ) } ) } |


    .h  |: ]] ]] ]] ]]  ]] ]] ]] ]]  ]] ]] ]] ]]   ]] ]] ]] ]] :|
    .s  |: }            )            }             )           :|
    .k  |: )            o/.      ]]  oo/ ]] ]] oo/ }           :|

    .h  |: ]] ]] ]] ]]  ]] ]] ]] ]]  ]] ]] ]] ]]   ]] ]] ]] ]] :|
    .s  |: }            )            }             )           :|
    .k  |: ]]. ]].  ]]  o/    ]] ]]  }             o/    ]     :|


    .h  |: ]] ]] ]] ]]  ]] ]] ]] ]]] ]]]  ]] ]]] ]]] ]] ]]  ]] ]] ]] ]] :|
    .s  |: }            )                 }                 ].       ]] :|
    .k  |: ]     ]] ]]  o/    ]] ]]       o/         ]      o/    ]     :|

    .h  |: ]] ]] ]] ]]  ]] ]] ]] ]]  ]] ]] ]] ]]   ]] ]] ]]] ]]] ]]] ]]] :|
    .s  |: }            )            }             ].            ]]      :|
    .k  |: ].       ]]  oo/  ].      o/    ]       }                     :|


    .h  |: ]] ]] ]] ]]  ]] ]] ]] ]]  ]] ]] ]] ]]   ]] ]] ]] ]] :|
    .s  |: }            )            }             ]]. ]].  ]] :|
    .k  |: ]]. ]].  ]]  }            ]]. ]].  ]]   }           :|

    .h  |: ]] ]] ]] ]]  ]] ]] ]] ]]  ]] ]] ]] ]]   ]] ]] ]] ]] :|
    .s  |: }            )            }             )           :|
    .k  |: ]]. ]].  ]]  o/.      ]]  ]] o/    ]]   }           :|


    .h  |: ]] ]] ]] ]]  ]] ]] ]] ]]  ]] ]] ]] ]]   ]] ]] ]] ]] :|
    .s  |: }            ]]. ]].  ]]  }             ].       ]] :|
    .k  |: )            }            ]     ]       }           :|

    .h  |: ]]] ]]] ]]] ]]] ]] ]]   ]] ]] ]] ]]] ]]]   ]] ]]] ]]] ]] ]]   ]] ]] ]] ]] :|
    .s  |: }                       )                  }                  )           :|
    .k  |: ]               ]       }                  o/         ]       oo/ ].      :|


    .h  | ))) |
    .s  | ))) |
    .k  | ))) |
